/*!
 *  Faydel Development Console
 *
 *  (c) Copyright 2019 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace FaydelConsole.Modules.Navigator;

use Core.UI.Render.CoreView;
use FaydelConsole.Modules.Navigator.Assets;
use FaydelConsole.Modules.Navigator.Manager;
use FaydelConsole.Modules.Navigator.Model;
use FaydelConsole.Modules.Navigator.NavigatorItem;
use FaydelConsole.Modules.Navigator.Editor.Editor;
use FaydelConsole.Modules.Navigator.Editor.EditorModel;
use Core.Components.Tabs.Tabs;
use Core.Components.Tabs.TabsItem;
use Core.Components.Tree.Tree;
use Core.Components.Tree.TreeModel;
use Core.Components.ContextMenu.ContextMenu;
use Core.Components.ContextMenu.ContextMenuItem;


struct View extends CoreView
{
	
	
	/**
	 * Returns manager name
	 */
	lambda string managerName() => classof Manager;
	
	
	
	/**
	 * Returns model name
	 */
	lambda string modelName() => classof Model;
	
	
	
	/**
	 * Required Assets
	 */
	lambda Collection<string> assets() =>
		[
			classof Assets,
		]
	;
	
	
	
	/**
	 * Required components
	 */
	lambda Collection<string> components() =>
		[
			classof Tabs,
			classof Tree,
			classof ContextMenu,
			classof Editor,
		]
	;
	
	
	
	/**
	 * Component css
	 */
	lambda string css(Dict<string> vars) => 
		@css{
			%console_main{
				padding: 0px;
				height: 100vh;
				width: 100%;
			}
			%console_menu{
				display: flex;
				flex-direction: row;
				justify-content: flex-start;
				align-items: center;
				height: 25px;
			}
			%console_content{
				display: table;
				width: 100%;
				height: calc(100% - 25px);
			}
			%console_left, %console_workspace{
				display: table-cell;
				vertical-align: top;
				height: 100%;
			}
			%console_left{
				position: relative;
				width: 300px;
			}
			%console_left %(Tree)tree{
				overflow: auto;
				height: calc(100% - 21px);
			}
			%console_workspace{
				position: relative;
			}
			%console_main %(Tree)tree_icon{
				color: slateblue;
			}
			%(Editor)console_editor{
				height: calc(100vh - 55px);
			}
		}
	;
	
	
	
	/**
	 * Returns icon by item
	 */
	pure string getIcon(NavigatorItem item)
	{
		if (item.kind == NavigatorItem::KIND_FOLDER)
		{
			if (item.is_open) return "fa fa-folder-open";
			return "fa fa-folder";
		}
		if (item.kind == NavigatorItem::KIND_FILE) return "fa fa-file";
		return "";
	}
	/* public static callback getIcon = method static::getIcon; */
	
	
	
	/**
	 * Component render
	 */
	lambda Collection<UIStruct> render(Model data, Collection<UIStruct> children) =>
		
		<div @key='main' @class='console_main' @control='console_main'>
			<div @class='console_menu'>
				<button @control='reload_button'>Reload</button>
				<button @control='save_button'>Save</button>
			</div>
			<div @class='console_content'>
				<div @class='console_left'>
					<Tree @model={ data.navigator } @key='navigator' @control='navigator' 
						get_icon={ method static::getIcon }></Tree>
				</div>
				<div @class='console_workspace'>
					{ static::renderWorkspace(data) }
				</div>
			</div>
		</div>
		<ContextMenu @model={ data.navigator_contextmenu } @key="navigator_contextmenu" @control="navigator_contextmenu">
			{ static::renderContextMenuItems(data) }
		</ContextMenu>
	;
	
	
	
	/**
	 * Render workspace
	 */
	pure Collection<UIStruct> renderWorkspace(Model data)
	{
		/* Find position by current_open_path */
		int active_pos = Model::getOpenEditorPos(data);
		
		mixed active_tag = null;
		if (active_pos >= 0)
		{
			active_tag = data.editors.item(active_pos);
		}
		
		return 
			<Tabs active={ "tab"~active_pos } @control='tabs'>
				{ data.editors.map( 
					lambda Collection<UIStruct> (EditorModel item, int index) =>
						<TabsItem key={ "tab"~index } title={ EditorModel::getTabTitle(item) } tag={ item }>
							<Editor @model={ item } @control='editor' index={ index }  path={ item.path_str }></Editor>
						</TabsItem>
				) }
			</Tabs>
		;
	}
	
	
	
	/**
	 * Render context menu items
	 */
	pure Collection<UIStruct> renderContextMenuItems(Model data)
	{
		if (data.navigator.selected == "")
			return "";
		
		Collection<int> path = TreeModel::splitPath( data.navigator.selected );
		NavigatorItem item = TreeModel::getTreeItem( data.navigator, path );
		
		if (item.kind == NavigatorItem::KIND_FOLDER)
		{
			return 
				<ContextMenuItem icon='fa fa-cogs' tag='create_file'>Создать файл</ContextMenuItem>
				<ContextMenuItem icon='fa fa-cogs' tag='create_directory'>Создать папку</ContextMenuItem>
				<ContextMenuItem icon='fa fa-trash' tag='delete_node'>Удалить</ContextMenuItem>
			;
		}
		else
		{
			return 
				<ContextMenuItem icon='fa fa-trash' tag='delete_node'>Удалить</ContextMenuItem>
			;
		}
	}
	
}
