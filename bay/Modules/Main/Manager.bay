/*!
 *  Bayrell Development Console
 *
 *  (c) Copyright 2018 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.bayrell.org/licenses/APACHE-LICENSE-2.0.html
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace BayrellConsole.Modules.Main;


use RuntimeUI.UIController;
use RuntimeUI.Annotations.BindValue;
use RuntimeUI.Annotations.BindModel;
use RuntimeUI.Annotations.Event;
use RuntimeUI.Annotations.EventAsync;
use RuntimeUI.Events.MouseEvent.MouseClickEvent;
use RuntimeUI.Events.UserEvent.ChangeEvent;
use RuntimeUI.Http.ApiRequest;
use RuntimeUI.Render.CoreManager;
use BayrellUI.ContextMenu.ContextMenuItemClickEvent;
use BayrellUI.Tree.TreeItem;
use BayrellUI.Tree.TreeItemSelectEvent;
use BayrellUI.Tree.TreeItemContextMenuEvent;
use BayrellUI.Tree.TreeItemDoubleClickEvent;
use BayrellUI.Tree.TreeModel;
use BayrellConsole.Models.NavigatorItem;
use BayrellConsole.Modules.Main.Model;


class Manager extends CoreManager
{
	public Model model;
	
	
	
	
	/* Controller console_main */
	
	@Event{ "event": classof MouseClickEvent, "method_name": "onConsoleMainClick" }
	public assignable UIController console_main = new UIController();
	
	
	/**
	 * Reload navigation
	 */
	public void onConsoleMainClick(MouseClickEvent event)
	{
		ContextMenuModel navigator_menu = this.model.navigator_menu;
		navigator_menu <= is_active <= false;
		this.updateModel({
			"navigator_menu": navigator_menu,
		});
	}
	
	
	
	
	
	/* Controller navigator */
	
	
	@BindModel{ "model": "navigator" }
	@Event{ "event": classof TreeItemContextMenuEvent, "method_name": "onItemContextMenu" }
	@EventAsync{ "event": classof TreeItemDoubleClickEvent, "method_name": "onItemDoubleClick" }
	public assignable UIController navigator = new UIController();
	
	
	
	/**
	 * Reload navigation
	 */
	public async void onItemDoubleClick(TreeItemDoubleClickEvent event)
	{
		
		/* Get path */
		Collection<string> path1 = rs::explode(".", event.path);
		Collection<int> path = path1.map( method rtl::toInt );
		
		/* Check is folder */
		TreeModel navigator = this.model.navigator;
		NavigatorItem item = TreeModel::getTreeItem(navigator, path);
		if (item.kind != NavigatorItem::KIND_FOLDER) 
		{
			return;
		}
		
		/* Get folder path */
		Collection<TreeItem> items = TreeModel::getTreePath(this.model.navigator, path);
		string path_str = "/" ~ rs::implode("/", items.map( lambda string (TreeItem item) => item.name ));
		
		/* Call api */
		ApiResult res = await ApiRequest::callApi(
			"BayrellConsoleApi.Navigator",
			"org.interfaces.Tree",
			"load",
			{
				"path": path_str,
			}
		);
		
		if (res.code == 1)
		{
			/* Update item */
			TreeModel navigator = this.model.navigator;
			NavigatorItem item = TreeModel::getTreeItem(navigator, path);
			item = item.copy({
				"items": res.result,
				"is_open": true,
			});
			navigator = TreeModel::setTreeItem(navigator, path, item);
			
			/* Update model */
			this.updateModel({
				"navigator": navigator
			});
		}
	}
	
	
	
	/**
	 * Reload navigation
	 */
	public void onItemContextMenu(TreeItemContextMenuEvent e)
	{
		e.event.cancel();
		
		ContextMenuModel navigator_menu = this.model.navigator_menu;
		navigator_menu = navigator_menu.copy({
			"is_active": true,
			"x": e.event.clientX,
			"y": e.event.clientY,
		});
		
		
		TreeModel navigator = this.model.navigator;
		navigator <= selected <= e.path;
		this.updateModel({
			"navigator": navigator,
			"navigator_menu": navigator_menu,
		});
		
	}
	
	
	
	
	
	/* Controller navigator_menu */
	
	
	@Event{ "event": classof ContextMenuItemClickEvent, "method_name": "onNavigatorMenuClick" }
	public assignable UIController navigator_menu = new UIController();
	
	
	
	/**
	 * Navigation manu item click
	 */
	public void onNavigatorMenuClick(ContextMenuItemClickEvent e)
	{
		e.event.cancel();
		
		console.log(e.tag);
		
		ContextMenuModel navigator_menu = this.model.navigator_menu;
		navigator_menu <= is_active <= false;
		this.updateModel({
			"navigator_menu": navigator_menu,
		});
	}
	
	
	
	
	/* Controller reload_button */
	
	@EventAsync{ "event": classof MouseClickEvent, "method_name": "onReload" }
	public assignable UIController reload_button = new UIController();
	
	
	
	/**
	 * Reload navigation
	 */
	public async void onReload(MouseClickEvent e)
	{
		
		ApiResult res = await ApiRequest::callApi(
			"BayrellConsoleApi.Navigator",
			"org.interfaces.Tree",
			"load",
			{}
		);
		
		/* Update navigator */
		TreeModel navigator = this.model.navigator;
		navigator <= items <= res.result;
		this.updateModel({
			"navigator": navigator
		});
	}
	
}