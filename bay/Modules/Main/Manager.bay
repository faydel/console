/*!
 *  Bayrell Development Console
 *
 *  (c) Copyright 2018 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.bayrell.org/licenses/APACHE-LICENSE-2.0.html
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace BayrellConsole.Modules.Main;


use RuntimeUI.UIController;
use RuntimeUI.Annotations.BindValue;
use RuntimeUI.Annotations.BindModel;
use RuntimeUI.Annotations.Event;
use RuntimeUI.Annotations.EventAsync;
use RuntimeUI.Events.MouseEvent.MouseClickEvent;
use RuntimeUI.Events.UserEvent.ChangeEvent;
use RuntimeUI.Http.ApiRequest;
use RuntimeUI.Render.CoreManager;
use BayrellUI.Tree.TreeItem;
use BayrellUI.Tree.TreeItemSelectEvent;
use BayrellUI.Tree.TreeItemDoubleClickEvent;
use BayrellUI.Tree.TreeModel;
use BayrellConsole.Modules.Main.Model;


class Manager extends CoreManager
{
	public Model model;
	
	
	@BindModel{ "model": "navigator" }
	@EventAsync{ "event": classof TreeItemDoubleClickEvent, "method_name": "onItemDoubleClick" }
	public assignable UIController navigator = new UIController();
	
	
	
	/**
	 * Reload navigation
	 */
	public async void onItemDoubleClick(TreeItemDoubleClickEvent e)
	{
		/* Get path */
		Collection<string> path1 = rs::explode(".", e.path);
		Collection<int> path = path1.map( method rtl::toInt );
		Collection<TreeItem> items = TreeModel::getTreeItems(this.model.navigator, path);
		string path_str = "/" ~ rs::implode("/", items.map( lambda string (TreeItem item) => item.name ));
		
		/* Call api */
		ApiResult res = await ApiRequest::callApi(
			"BayrellConsoleApi.Navigator",
			"org.interfaces.Tree",
			"load",
			{
				"path": path_str,
			}
		);
		
		if (res.code == 1)
		{
			/* Update model */
			TreeModel navigator = TreeModel::updateItems(this.model.navigator, path, res.result.items);
			this.updateModel({
				"navigator": navigator
			});
		}
	}
	
	
	
	
	@EventAsync{ "event": classof MouseClickEvent, "method_name": "onReload" }
	public assignable UIController reload_button = new UIController();
	
	
	
	/**
	 * Reload navigation
	 */
	public async void onReload(MouseClickEvent e)
	{
		
		ApiResult res = await ApiRequest::callApi(
			"BayrellConsoleApi.Navigator",
			"org.interfaces.Tree",
			"load",
			{}
		);
		
		this.updateModel({
			"navigator": res.result
		});
	}
	
}